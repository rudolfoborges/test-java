buildscript {
    ext {
        springBootVersion = '1.5.4.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'org.springframework.boot'

    sourceCompatibility = 1.8

    repositories {
        mavenCentral()
    }

    ext {
        springCloudVersion = 'Dalston.SR2'
    }

    dependencies {
        compile('org.springframework.boot:spring-boot-starter-actuator')
        compile('org.springframework.boot:spring-boot-actuator-docs')
        compile('org.springframework.cloud:spring-cloud-starter-hystrix')
        compile('org.springframework.cloud:spring-cloud-starter-hystrix-dashboard')
        compile('org.springframework.boot:spring-boot-starter-data-jpa')
        compile('org.springframework.boot:spring-boot-starter-validation')
        compile('org.springframework.boot:spring-boot-starter-web')
        testCompile('org.springframework.boot:spring-boot-starter-test')
        compile(group: 'org.jolokia', name: 'jolokia-core')

        compile('org.springframework.cloud:spring-cloud-starter-eureka')

        compile(group: 'org.springframework.cloud', name: 'spring-cloud-starter-sleuth')
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }
}


project(':user-api') {

    bootRun {
        systemProperties = System.properties
    }

    jar{
        archiveName = 'user-api.jar'
    }

    dependencies {
        compile project(':user-core')

        compile('org.springframework.boot:spring-boot-starter-actuator')
        compile('org.springframework.boot:spring-boot-actuator-docs')
        compile('org.springframework.boot:spring-boot-starter-web')

        compile(group: 'io.springfox', name: 'springfox-swagger-ui', version: '2.6.1')
        compile(group: 'io.springfox', name: 'springfox-swagger2', version: '2.6.1')

        compile(group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310')
    }

}

project(':user-core') {

    bootRepackage {
        enabled = false
    }

    dependencies {
        compile project(':user-persistence')
        compile project(':user-messaging')
    }

}

project(':user-persistence') {

    bootRepackage {
        enabled = false
    }

    dependencies {
        compile('org.springframework.boot:spring-boot-starter-data-jpa')
        compile('org.springframework.boot:spring-boot-starter-validation')
        runtime('mysql:mysql-connector-java')
    }

}

project(':user-messaging') {

    bootRepackage {
        enabled = false
    }

    dependencies {
        compile(group: 'org.springframework.boot', name: 'spring-boot-starter-amqp')
        compile('org.springframework.cloud:spring-cloud-starter-stream-rabbit')
        compile(group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310')

        // Feign - HTTP Client por Anotação
        compile(group: 'org.springframework.cloud', name: 'spring-cloud-starter-feign')

        // HTTP Client do Feign
        compile(group: 'io.github.openfeign', name: 'feign-httpclient')

        compile project(':user-persistence')
    }


}


